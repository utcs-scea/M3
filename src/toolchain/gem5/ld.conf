#if defined(__x86_64__)
OUTPUT_FORMAT("elf64-x86-64")
#elif defined(__arm__)
OUTPUT_FORMAT("elf32-littlearm")
#elif defined(__aarch64__)
OUTPUT_FORMAT("elf64-littleaarch64")
#endif

ENTRY(_start)

PHDRS
{
    text    PT_LOAD;
    data    PT_LOAD;
    bss     PT_LOAD;
}

SECTIONS
{
    . = 0x100000;

    .text :
    {
        PROVIDE(_text_start = .);
        *(.text*)
        *(.ARM.extab.text*)
    } : text = 0x90909090

    .rodata :
    {
        *(.rodata*)
        PROVIDE(_text_end = .);
    } : text

#if defined(__arm__)
    .ARM.exidx :
    {
        __exidx_start = .;
        *(.ARM.exidx* .gnu.linkonce.armexidx.*)
        __exidx_end = .;
    } : text
#endif

    .data ALIGN(0x1000) :
    {
        PROVIDE(_data_start = .);
        *(.data)
        *(.data.*)
    } : data

    .ctors :
    {
        CTORS_BEGIN = .;
        KEEP(*(SORT(.init_array.*)))
        KEEP(*(.init_array))
        CTORS_END = .;
    } : data

    .bss ALIGN(0x1000) :
    {
        *(COMMON*)
        *(.bss*)
        PROVIDE(_bss_end = .);
    } : bss
}
